;8255片选接IOY0,8254片选接IOY1
A_8255 EQU 0600H
B_8255 EQU 0602H
C_8255 EQU 0604H
CONTROL_8255 EQU 0606H

COUNTER0 EQU 0640H
COUNTER1 EQU 0642H
COUNTER2 EQU 0644H
CONTROL_8254 EQU 0646H

DATA SEGMENT
TABLE1:
	DB 3FH;0
	DB 06H;1
	DB 5BH;2
	DB 4FH;3
	DB 66H;4
	DB 6DH;5
	DB 7DH;6
	DB 07H;7
	DB 7FH;8
	DB 6FH;9
DATA ENDS

CODE SEGMENT
	ASSUME CS:CODE, DS:DATA
START:
	MOV AX,DATA
	MOV DS,AX
	
    MOV AX, OFFSET MIR6
    MOV SI, 0038H
    MOV [ES:SI], AX
    MOV AX, CS
    MOV SI, 003AH
    MOV [ES:SI], AX
	
    CLI
    MOV AL, 11H
    OUT 20H, AL
    MOV AL, 08H
    OUT 21H, AL
    MOV AL, 04H
    OUT 21H, AL
    MOV AL, 03H
    OUT 21H, AL
    MOV AL, 3FH
    OUT 21H, AL
    STI
    
	MOV DX, CONTROL_8254;8254
    MOV AL, 76H  ;01110110B;选择计数器1,读写格式是先低8位,再高8位,方式3,计数码制选择2进制
    OUT DX, AL 
    MOV DX, COUNTER1 
    MOV AL, 00H
    OUT DX, AL
    MOV AL, 48H  ;
    OUT DX, AL
	
	LEA SI,TABLE1
	MOV DX,CONTROL_8255
	MOV AL,89H ;10001001B  A,B口输出，C口输入
	OUT DX,AL
	
	MOV DX,B_8255
	MOV AL,3FH;数码管的段选码，意思是让数码管显示0
	OUT DX,AL
	MOV DX,A_8255
	MOV AL,00H;数码管的位选码，意思是选择所有的数码管
	OUT DX,AL
	
	MOV AH,11111110B;位选码，选择最左侧的数码管	
BEGIN:	
    MOV CX,0AH;循环10次
	MOV BX,0001H;BX是数组偏移
AA1:
	PUSH CX
	MOV CX,03H;循环3次，从右向左依次显示1,2,3
    MOV AL,AH
AA2:
	CALL LIGHT
	ROL AL,1;循环左移1位
	CMP AL,10111111B;看是不是到最右边了
	JNZ B11
	MOV AL,11111110B
B11:
	INC BX
	LOOP AA2
	
	POP CX
	MOV BX,0001H    ;恢复BX的值
	LOOP AA1
	JMP BEGIN

	
LIGHT:
	;AL:POSITION
	;BX:CONTENT
	MOV DX,A_8255
	OUT DX,AL
	PUSH AX
	MOV AL,[BX+SI]
	MOV DX,B_8255
	OUT DX,AL
	POP AX
	CALL DELAY
	RET
	
MIR6:
	ROL AH,1;位选码循环左移1位，也就是起始位置向右移动1位
	CMP AH,10111111B
	JNZ ENDI
	MOV AH,11111110B
ENDI:
	IRET
	
DELAY:
	PUSH CX
	MOV CX,0A00H
	LOOP $
	POP CX
	RET
CODE ENDS
	END START


